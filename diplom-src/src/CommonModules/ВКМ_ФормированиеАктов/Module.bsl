#Область ПрограммныйИнтерфейс
//Процедура формирует акты за период
//Параметры:
//        Период - Дата- Период формирования актов;
//        АдресХранилища - ХранилищеЗначения - ;
Процедура ВКМ_СФормироватьАктыЗаПериод(Период, АдресХранилища) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ВложенныйЗапрос.Ссылка КАК Реализация,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.Организация КАК Организация
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|			РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|			РеализацияТоваровУслуг.Договор КАК Договор
	|		ИЗ
	|			Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ГДЕ
	|			РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания) КАК ВложенныйЗапрос
	|		ПО ДоговорыКонтрагентов.Ссылка = ВложенныйЗапрос.Договор
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
	|	И ВложенныйЗапрос.Ссылка ЕСТЬ NULL
	|	И КОНЕЦПЕРИОДА(ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействия, ДЕНЬ) >= &ДатаОкончания
	|	И НАЧАЛОПЕРИОДА(ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействия, ДЕНЬ) <= &ДатаНачала";

	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Период));

	Выборка = Запрос.Выполнить().Выбрать();

	ВсегоДокументов = Выборка.Количество();
	ТекДок = 0;
	Пока Выборка.Следующий() Цикл
		ДокОбъект = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		ДокОбъект.Дата = КонецМесяца(Период);
		ДокОбъект.Организация = Выборка.Организация;
		ДокОбъект.Контрагент = Выборка.Контрагент;
		ДокОбъект.Договор = Выборка.Договор;
		ДокОбъект.Основание = Выборка.Договор;
		ДокОбъект.Ответственный = Пользователи.ТекущийПользователь();
		ДокОбъект.Комментарий = "Создан автоматической обработкой";
		ДокОбъект.Записать();

		ДокОбъект.ВКМ_ВыполнитьАвтозаполнение(ДокОбъект.Ссылка);

		ДокОбъект.ПроверитьЗаполнение();

		ДокОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);

		ПроцентВыполнения = (ТекДок / ВсегоДокументов) * 100;
		ПроцентВыполнения = Окр(ПроцентВыполнения, 0);

		ДлительныеОперации.СообщитьПрогресс(ПроцентВыполнения, СокрЛП(ДокОбъект.Ссылка));
		ТекДок = ТекДок + 1;
		ПоместитьВоВременноеХранилище(Новый Структура("ТекДок, ВсегоДокументов", ТекДок, ВсегоДокументов),
			АдресХранилища);
	КонецЦикла;

КонецПроцедуры
#КонецОбласти