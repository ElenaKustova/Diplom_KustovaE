#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ПериодПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ВКМ_СформироватьАктыЗапериод(Команда)
	Если Не ЗначениеЗаполнено(Период) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Период не заполнен";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;

	Индикатор = 0;

	АдресХранилища = ПоместитьВоВременноеХранилище(Новый Структура("ТекДок, ВсегоДокументов", 0, 1),
		УникальныйИдентификатор);

	ПараметрыЗапуска = Новый Структура;
	ПараметрыЗапуска.Вставить("Период", Период);
	ДлительнаяОперация = НачатьВыполнениеДолгойФункции(ПараметрыЗапуска, АдресХранилища);

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершениеДлительнойОперации", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении);

	СформироватьСписокДокументов();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПериодПриИзмененииНаСервере()
	СформироватьСписокДокументов();
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокДокументов()

	Перем Запрос;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Реализация,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Договор КАК Договор
	|ПОМЕСТИТЬ ВТ_Док
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ПОМЕСТИТЬ ВТ_Договор
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
	|	И КОНЕЦПЕРИОДА(ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействия, ДЕНЬ) >= &ДатаОкончания
	|	И НАЧАЛОПЕРИОДА(ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействия, ДЕНЬ) <= &ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Договор.Договор КАК Договор,
	|	ВТ_Док.Реализация КАК Реализация
	|ИЗ
	|	ВТ_Договор КАК ВТ_Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Док КАК ВТ_Док
	|		ПО ВТ_Договор.Договор = ВТ_Док.Договор";

	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Период));
	Объект.СписокДоговоров.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

&НаСервере
Функция НачатьВыполнениеДолгойФункции(ПараметрыЗапуска, АдресХранилища)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование актов за период'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
		"ВКМ_ФормированиеАктов.ВКМ_СФормироватьАктыЗаПериод", ПараметрыЗапуска.Период, АдресХранилища);

КонецФункции

&НаКлиенте
Процедура ЗавершениеДлительнойОперации(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение( , Результат.КраткоеПредставлениеОшибки);
	Иначе
		ОбработчикОжидания();
		УдалитьИзВременногоХранилища(АдресХранилища);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжидания() Экспорт

	ДанныеОВыполнении = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(ДанныеОВыполнении) = Тип("Структура") Тогда
		Индикатор = ДанныеОВыполнении.ТекДок * 100 / ДанныеОВыполнении.ВсегоДокументов;
		Если ДанныеОВыполнении.ТекДок <> ДанныеОВыполнении.ВсегоДокументов Тогда
			ПодключитьОбработчикОжидания("ОбработчикОжидания", 5, Истина);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти