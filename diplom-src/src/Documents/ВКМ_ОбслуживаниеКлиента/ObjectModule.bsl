#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту КАК ЧасыКОплатеКлиенту,
	|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧаса КАК ВКМ_СтоимостьЧаса,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот КАК ПроцентОтРабот,
	|	ВКМ_ОбслуживаниеКлиента.Клиент КАК Клиент
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ВКМ_ОбслуживаниеКлиента.Договор = ДоговорыКонтрагентов.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(
	|					&Период,
	|					Сотрудник В
	|						(ВЫБРАТЬ
	|							ВКМ_ОбслуживаниеКлиента.Специалист КАК Сотрудник
	|						ИЗ
	|							Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
	|						ГДЕ
	|							ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|			ПО ВКМ_ОбслуживаниеКлиента.Специалист = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
	|		ПО ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка = ВКМ_ОбслуживаниеКлиента.Ссылка
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиента.Дата МЕЖДУ ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействия И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействия
	|	И ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка = &Ссылка
	|ИТОГИ
	|	СУММА(ЧасыКОплатеКлиенту),
	|	МАКСИМУМ(ВКМ_СтоимостьЧаса),
	|	МАКСИМУМ(ПроцентОтРабот)
	|ПО
	|	Клиент";

	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаКлиент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Если ВыборкаКлиент.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Дата документа не соответствует договору", ЭтотОбъект, "Договор",
			Договор, Истина);
		Возврат;
	КонецЕсли;

	ВыборкаКлиент.Следующий();

	Если Не ЗначениеЗаполнено(ВыборкаКлиент.ПроцентОтРабот) Тогда 
		СообщениеПользователю = СтрШаблон("Специалисту не установлен процент оплаты от работ %1", Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить());
		ОбщегоНазначения.СообщитьПользователю(СообщениеПользователю, ЭтотОбъект, "Специалист", Специалист, Отказ);
		Возврат;
	КонецЕсли;

	Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
	Движение.Период = Дата;
	Движение.Клиент = Клиент;
	Движение.Договор = Договор;
	Движение.КоличествоЧасов = ВыборкаКлиент.ЧасыКОплатеКлиенту;
	СуммаКОплате = ВыборкаКлиент.ВКМ_СтоимостьЧаса * Движение.КоличествоЧасов;
	Движение.СуммаКОплате = СуммаКОплате;

	Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
	Движение.Период = Дата;
	Движение.Сотрудник = Специалист;
	Движение.ЧасовКОплате = ВыборкаКлиент.ЧасыКОплатеКлиенту;
	Движение.СуммаКОплате = СуммаКОплате * ВыборкаКлиент.ПроцентОтРабот / 100;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Ссылка.Пустая() Тогда
		СпрОбъект = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		СпрОбъект.ТекстСообщения = СтрШаблон(
			"Уважаемый %1, поступила новая заявка от клиента: Дата проведения работ %2, с %3 до %4.", Специалист,
			Формат(ДатаПроведенияРабот, "ДФ=dd.MM.yyyy;"), Формат(ВремяНачалаРаботПлан, "ДФ=HH.mm;"), Формат(
			ВремяОкончанияРаботПлан, "ДФ=HH.mm;"));
		СпрОбъект.Записать();
	Иначе

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.Дата КАК Дата,
		|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
		|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРаботПлан КАК ВремяНачалаРаботПлан,
		|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРаботПлан КАК ВремяОкончанияРаботПлан,
		|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка";

		Запрос.УстановитьПараметр("Ссылка", Ссылка);

		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();

		Если Выборка.Следующий() Тогда
			ДокДатаПроведенияРабот = Выборка.ДатаПроведенияРабот;
			ДокВремяНачалаРаботПлан = Выборка.ВремяНачалаРаботПлан;
			ДокВремяОкончанияРаботПлан = Выборка.ВремяОкончанияРаботПлан;
			ДокСпециалист = Выборка.Специалист;
		КонецЕсли;

		Если ДокДатаПроведенияРабот <> ДатаПроведенияРабот Тогда
			Текст1 = " дата проведения работ изменилась на " + Формат(ДатаПроведенияРабот, "ДФ=dd.MM.yyyy");
		КонецЕсли;

		Если ДокВремяНачалаРаботПлан <> ВремяНачалаРаботПлан Тогда
			Текст2 = " время начала работ изменилось на " + Формат(ВремяНачалаРаботПлан, "ДФ=HH.mm");
		КонецЕсли;

		Если ДокВремяОкончанияРаботПлан <> ВремяОкончанияРаботПлан Тогда
			Текст3 = " время окончания работ изменилось на " + Формат(ВремяОкончанияРаботПлан, "ДФ=HH.mm");
		КонецЕсли;

		Текст = "" + Текст1 + ?(Не ПустаяСтрока(Текст2) И Не ПустаяСтрока(Текст1), ",", "") + Текст2 + ?(
			Не ПустаяСтрока(Текст3) И (Не ПустаяСтрока(Текст1) Или Не ПустаяСтрока(Текст2)), ",", "") + Текст3;

		Если Не ПустаяСтрока(Текст) Тогда
			СпрОбъект = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
			СпрОбъект.ТекстСообщения = СтрШаблон("Уважаемый %1, в вашем заказе произошли изменения:" + Текст + ".",
				Специалист);
			СпрОбъект.Записать();
		КонецЕсли;

		Если ДокСпециалист <> Специалист Тогда
			ТекстОНовомЗаказе = СтрШаблон(
			"Уважаемый %1, Вам передан заказ: Дата проведения работ %2, с %3 до %4.", Специалист,
				Формат(ДатаПроведенияРабот, "ДФ=dd.MM.yyyy;"), Формат(ВремяНачалаРаботПлан, "ДФ=HH.mm;"), Формат(
			ВремяОкончанияРаботПлан, "ДФ=HH.mm;"));
			Если Не ПустаяСтрока(ТекстОНовомЗаказе) Тогда
				СпрОбъект = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
				СпрОбъект.ТекстСообщения = ТекстОНовомЗаказе;
				СпрОбъект.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли