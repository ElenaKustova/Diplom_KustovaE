#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	РассчитатьДополнительныеНачисления();
	РассчитатьНДФЛ(); 
	ВКМ_ВзаиморасчетыССотрудниками();
	
КонецПроцедуры  

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РассчитатьДополнительныеНачисления()
   	
	Для Каждого ТекСтрокаНачисления Из СписокСотрудников Цикл
		Движение = Движения.ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь; 
		Движение.Сотрудник = ТекСтрокаНачисления.Сотрудник;
		Движение.ВидРасчета = ПланыВидовРасчета.ДополнительныеНачисления.ФиксированнаяПремия;
		Движение.ПериодРегистрации = Дата;
		Движение.Сумма = ТекСтрокаНачисления.СуммаПремии;
		
		Движение = Движения.Удержания.Добавить();
		Движение.Сторно = Ложь; 
		Движение.ПериодРегистрации = НачалоМесяца(Дата); 
		Движение.ВидРасчета = ПланыВидовРасчета.Удержания.НДФЛ;
		Движение.Сотрудник = ТекСтрокаНачисления.Сотрудник;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		Движение.Показатель = 13;
	КонецЦикла; 
	
	Движения.ДополнительныеНачисления.Записать();
	Движения.Удержания.Записать();

КонецПроцедуры

Процедура РассчитатьНДФЛ()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УдержанияБазаДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	УдержанияБазаДополнительныеНачисления.СуммаБаза КАК СуммаБаза
		|ИЗ
		|	РегистрРасчета.Удержания.БазаДополнительныеНачисления(
		|			&Измерения,
		|			&Измерения,
		|			,
		|			Регистратор = &Ссылка
		|				И ВидРасчета = &НДФЛ) КАК УдержанияБазаДополнительныеНачисления";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения); 
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.Удержания.НДФЛ);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
		
	Пока Выборка.Следующий() Цикл
		Движение = Движения.Удержания[Выборка.НомерСтроки - 1];
		Сумма = Выборка.СуммаБаза * Движение.Показатель / 100; 
		Движение.Сумма = Сумма; 
	КонецЦикла; 
		
	Движения.Удержания.Записать();	
	
КонецПроцедуры

Процедура ВКМ_ВзаиморасчетыССотрудниками()
	
	Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СУММА(ДополнительныеНачисления.Сумма) КАК НачисленияСумма,
			|	ДополнительныеНачисления.Сотрудник КАК Сотрудник
			|ПОМЕСТИТЬ ВременнаяТаблица
			|ИЗ
			|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
			|ГДЕ
			|	ДополнительныеНачисления.Регистратор = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ДополнительныеНачисления.Сотрудник
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МАКСИМУМ(Удержания.Сумма) КАК УдержанияСумма,
			|	Удержания.Сотрудник КАК Сотрудник,
			|	ВременнаяТаблица.НачисленияСумма КАК НачисленияСумма
			|ИЗ
			|	ВременнаяТаблица КАК ВременнаяТаблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.Удержания КАК Удержания
			|		ПО ВременнаяТаблица.Сотрудник = Удержания.Сотрудник
			|ГДЕ
			|	Удержания.Регистратор = &Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	Удержания.Сотрудник,
			|	ВременнаяТаблица.НачисленияСумма";
			
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Сотрудник = Выборка.Сотрудник;
			Движение.Сумма = Выборка.НачисленияСумма - Выборка.УдержанияСумма; 
		КонецЦикла; 
		
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();

КонецПроцедуры

#КонецОбласти

#КонецЕсли
