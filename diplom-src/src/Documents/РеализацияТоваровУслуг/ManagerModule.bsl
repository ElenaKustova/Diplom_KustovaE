
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции
//Доработка++

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт об оказанных услугах
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанныхУслугахMXL";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'");
	
	// Акт об оказанных услугах Word
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанныхУслугахDOCX";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'"); 
	КомандаПечати.Картинка = БиблиотекаКартинок.ФорматWord2007;
		
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОказанныхУслугахMXL");
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ТабличныйДокумент = ПечатьАктОбОказанныхУслугахMXL(МассивОбъектов, ОбъектыПечати);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказанных услугах'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОбОказанныхУслугах";
	КонецЕсли;
		
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОказанныхУслугахDOCX");
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ОфисныеДокументы = ПечатьАктОбОказанныхУслугахDOCX(МассивОбъектов);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказанных услугах'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_DOC_АктОбОказанныхУслугах";
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАктОбОказанныхУслугахMXL(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АктОбОказанныхУслугах";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_АктОбОказанныхУслугах"); 
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов); 
	
	ПервыйДокумент = Истина; 
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1; 
		
		ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиТаблица(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиПодвал(ДанныеДокументов, ТабличныйДокумент, Макет); 
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
	
	Возврат ТабличныйДокумент;
	
КонецФункции         

Функция ПечатьАктОбОказанныхУслугахDOCX(МассивОбъектов)
	
	ОфисныеДокументы = Новый Соответствие;
	
	ДвоичныеДанныеМакета = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_DOC_АктОбОказанныхУслугах");
	Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(ДвоичныеДанныеМакета, Неопределено);
	
	ОписаниеОбластей = Новый Структура; 
	
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей,"Заголовок", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей,"ШапкаТаблицы", "Общая");
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей,"СтрокаТаблицы", "СтрокаТаблицы"); 
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей,"Подвал", "Общая");  
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);  
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(Неопределено, Неопределено, Макет);
		
		ОбластьЗаголовок = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.Заголовок); 
		
		ДанныеПечати = Новый Структура;  
		ДанныеПечати.Вставить("Дата", Формат(ДанныеДокументов.Дата, "ДФ=dd.MM.yyyy")); 
		ДанныеПечати.Вставить("Номер", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер));
		ДанныеПечати.Вставить("Организация", ДанныеДокументов.Организация);
		ДанныеПечати.Вставить("Контрагент", ДанныеДокументов.Контрагент);
		ДанныеПечати.Вставить("Договор", ДанныеДокументов.Договор);
        ДанныеПечати.Вставить("Ссылка", ДанныеДокументов.Ссылка);

		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, ОбластьЗаголовок, ДанныеПечати);
		
		ОбластьШапкаТаблицы = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.ШапкаТаблицы); 					
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, ОбластьШапкаТаблицы, ДанныеПечати); 
		
	    ДанныеПечатиТовары = ОбщегоНазначения.ТаблицаЗначенийВМассив(ДанныеПечати.Ссылка.Услуги.Выгрузить());
		ОбластьСтрокаТаблицы = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.СтрокаТаблицы);
		УправлениеПечатью.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, ОбластьСтрокаТаблицы, ДанныеПечатиТовары, Ложь);  	
			
		ОбластьПодвал = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.Подвал);
		
		ДанныеПечати.Вставить("Ответственный", ДанныеДокументов.Ответственный);
		Сумма = ДанныеДокументов.СуммаДокумента;
	    СуммаПрописью = ЧислоПрописью(Сумма,"Л=ru_RU; ДП=Ложь","рубль, рубля, рублей, м, копейка, копейки, копеек, ж" );
		ДанныеПечати.Вставить("СуммаДокумента", СуммаПрописью);
				
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, ОбластьПодвал, ДанныеПечати); 
				
		АдресПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма); 
		
		ОфисныеДокументы.Вставить(АдресПечатнойФормы, ДанныеДокументов.Ссылка);
		УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
		
		
	КонецЦикла;	
	
	УправлениеПечатью.ОчиститьСсылки(Макет);
	
	Возврат ОфисныеДокументы;
	
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Номер КАК Номер,
	               |	РеализацияТоваровУслуг.Дата КАК Дата,
	               |	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	               |	РеализацияТоваровУслуг.Организация КАК Организация,
	               |	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	               |	РеализацияТоваровУслуг.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги,
	               |	РеализацияТоваровУслуг.Договор КАК Договор,
	               |	РеализацияТоваровУслуг.Ответственный КАК Ответственный,
	               |	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Ссылка В(&МассивОбъектов)"; 
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов); 
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	Возврат Выборка;	 
	
КонецФункции

Процедура ВывестиЗаголовок(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок"); 
	
	ДанныеПечати = Новый Структура;
	ШаблонЗаголовка = "Акт об оказанных услугах №%1 от %2 г.";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
	ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
	Формат(ДанныеДокументов.Дата, "ДФ=dd.MM.yyyy"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	ДанныеПечати.Вставить("Организация", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Контрагент", ДанныеДокументов.Контрагент);
	ДанныеПечати.Вставить("Договор", ДанныеДокументов.Договор); 
		 
	ОбластьЗаголовок.Параметры.Заполнить(ДанныеПечати);
	ТабличныйДокумент.Вывести(ОбластьЗаголовок); 
	
КонецПроцедуры

Процедура ВывестиТаблица(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();
	Пока ВыборкаУслуги.Следующий() Цикл
		ОбластьСтрокаТаблицы.Параметры.Заполнить(ВыборкаУслуги);
		ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиПодвал(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ДанныеПечати = Новый Структура; 
	
	Сумма = ДанныеДокументов.СуммаДокумента;
	СуммаПрописью = ЧислоПрописью(Сумма,"Л=ru_RU; ДП=Ложь","рубль, рубля, рублей, м, копейка, копейки, копеек, ж" );
	ДанныеПечати.Вставить("СуммаДокумента", СуммаПрописью);
    ДанныеПечати.Вставить("Ответственный", ДанныеДокументов.Ответственный);
	ОбластьПодвал.Параметры.Заполнить(ДанныеПечати);

	ТабличныйДокумент.Вывести(ОбластьПодвал);
	
КонецПроцедуры   

#КонецОбласти 
//Доработка--

#КонецЕсли